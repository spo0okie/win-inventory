<job id="logon_Descr">
<script language="VBScript" src="../libs/lib_core.vbs" ></script>
<script language="VBScript" src="../libs/lib_http.vbs" ></script>
<script language="VBScript" src="../libs/lib_aduser.vbs" ></script>
<script language="VBScript" src="../libs/lib_time.vbs" ></script>
<script language="VBScript" src="../config.priv.vbs" ></script>
<script language="javascript" runat="server" src="../libs/lib_url.js" ></script>
<script language="VBScript">

'Скрипт фиксирования регистрации пользователя на ПК в БД инвентаризации. 
'Работает не так тупо как раньше - просто добавить запись в БД с текущим временем или попытаться через 5 минут.
'Скрипт определяет время логина пользователя и пытается класть в БД именно набор логин+комп+время
'и кладет только если такого набора там еще нет
'v0.4 * функцонинал XML парсинга и HTTP REST вынесен в lib_http, URL.encdode/decode -> lib_url
'v0.3 - вынесено в отдельный скрипт относительно скрипта логона, для запуска по шедулеру в любое время
'v0.2 - переведено на WSH для подключения библиотек и ведения лога
const scrName="gp_logonInventory" : const scrVer="0.5"
Dim adsinfo, logItems, oShell, strSessionName, vbsTimestamp, strTimestamp, unixTimestamp

'if ( inStr(LCase(SessionName),"rdp") <> 0 ) Then
'	wscript.quit
'End If


logFile = WorkDir & scrName & ".log"
Msg "-" : Msg "Script started: "&scrName&" "&scrVer

Set wshNetwork = CreateObject( "WScript.Network" )
Set objWMIService = GetObject("winmgmts:\\.\root\cimv2")
vbsTimestamp = timeLogonVbs
if (isnull(vbsTimestamp)) then
	halt "Error fetchin logon time"
end if

strTimestamp = timeVbsToTimestamp(vbsTimestamp)
unixTimestamp = timeVbsToUnix(vbsTimestamp)
Msg strTimestamp & "(" & unixTimeStamp & ")"

'ищет запись этого логона в БД возвращает ID созданной/обновленной записи
function setRecord(byVal comp, byVal name, byVal logonTime)
	data="comp_name=" & comp & "&user_login=" & name & "&time=" & logonTime
	
	Msg "Searchin record id of " & data & " ..."
	res=getXmlData (inventory_apihost & "/web/api/login-journal/search?" & data)
	if (getXmlResponseID(res) > -1) then
		updRecord="ok "& res
		Msg "Record already set: " & res & "; Database OK"
	else
	
		Msg "Record not found. Creatin new rec..."
		res=postXmlData(inventory_apihost & "/web/api/login-journal",data)
		if (getXmlResponseID(res) > -1) then
			updRecord="ok " & res
			Msg "Data sent: " & res & "; Database updated"
		else
			Msg "Error sending data ... retrying in next launch"
			Msg data
			Msg res
		end if
		
	end if
	
end function


'Class: Win32_ComputerSystem
'Property: DomainRole

'Data type: uint16
'Access type: Read-only

'Role of a computer in an assigned domain workgroup. 
'A domain workgroup is a collection of computers on the same network. 
'For example, a DomainRole property may show that a computer is a member workstation. 

'Value Meaning
'0 (0x0) Standalone Workstation
'1 (0x1) Member Workstation
'2 (0x2) Standalone Server
'3 (0x3) Member Server
'4 (0x4) Backup Domain Controller
'5 (0x5) Primary Domain Controller
Set colItems = objWMIService.ExecQuery("Select * from Win32_ComputerSystem",,48)
For Each objItem in colItems
    strComputerDomain = objItem.Domain
    if (objItem.DomainRole>1) then
		Msg  "server detected"
		wscript.quit
    End if
Next


strUserDomain = wshNetwork.UserDomain
'WScript.Echo "User: " & strUserDomain & "\" & oUser.sAMAccountName
'WScript.Echo "Comp: " & strComputerDomain & "\" & ThisComp.cn

on error resume next
res=setRecord (strComputerDomain & "\" & objComputer.cn , strUserDomain & "\" & objUser.sAMAccountName, unixTimestamp)
Msg "Script complete."

wscript.quit
</script>
</job>