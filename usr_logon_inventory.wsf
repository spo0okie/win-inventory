<job id="logon_Descr">
<script language="VBScript" src="../libs/lib_core.vbs" ></script>
<script language="VBScript" src="../libs/lib_http.vbs" ></script>
<script language="VBScript" src="../libs/lib_time.vbs" ></script>
<script language="VBScript" src="../config.priv.vbs" ></script>
<script language="javascript" runat="server" src="../libs/lib_url.js" ></script>
<script language="VBScript">
Option explicit
DEBUGMODE=0

'Скрипт фиксирования регистрации пользователя на ПК в БД инвентаризации. 
'Работает не так тупо как раньше - просто добавить запись в БД с текущим временем или попытаться через 5 минут.
'Скрипт определяет время логина пользователя и пытается класть в БД именно набор логин+комп+время
'и кладет только если такого набора там еще нет
'v0.9 * обязательная авторизация. работа с новым API со слепым PUSH данных, без чтения (необходим минимальный набор прав)
'v0.8.5 * WMI запрос завернут в игнор ошибок, чтобы не выбрасывать их пользователю
'v0.8.4 * Добавлена отладка, удален лишний объект WMI
'v0.8.3 * Попытка обработать ошибки доступа
'v0.8.2 ! защита от отправки данных со сбитыми часами (не отправляем данные старее 30 дней)
'v0.8.1 * небольшие фиксы. выкинута библиотека lib_aduser, т.к. она выбрасывала ошибку при недоступности контроллера домена (инициируется с запросами к GC/LDAP)
'		все данные мы получаем через WMI запрос, потому либу убрал за ненадобностью
'		НО по факту при отсутствии связи с контроллером домена WMI запрос не может вернуть имя пользователя инициировавшего сессию
'v0.8	! оказалось что при запуске WMI запроса с правами админа, он отдает журнал входов всех пользователей. В связи с этим на каждую запись в журнале дополнительно запрашиваем
'		пользоветеля, который инициировал сессию (чтобы не заявить что все сессии инициированы пользователем-админом, т.к. по факту это не так.)
'		также скидываем привилегии при старте через launchpad
'v0.7 	+ отправляет локальное время ПК для обнаружения сдвига времени ПК относительно времени сервера и для правки времени логона в обратную сторону
'v0.6 	+ отправляет и RDP входы, но указывает в журнале что это именно RDP
'v0.5 	* отправляет время из журнала WMI, а не из момента запуска ПК
'v0.4 	* функцонинал XML парсинга и HTTP REST вынесен в lib_http, URL.encdode/decode -> lib_url
'v0.3 	- вынесено в отдельный скрипт относительно скрипта логона, для запуска по шедулеру в любое время
'v0.2 	- переведено на WSH для подключения библиотек и ведения лога
'unPrivelegeMe
const scrName="gp_logonInventory" : const scrVer="0.8.5"
Dim adsinfo, logItems, oShell, strSessionName, vbsTimestamp, strTimestamp, unixTimestamp


'Logon Types
'0 Used only by the System account.
'Interactive (2) Intended for users who are interactively using the machine, such as a user being logged on by a terminal server, remote shell, or similar process.
'Network (3) Intended for high-performance servers to authenticate clear text passwords. LogonUser does not cache credentials for this logon type.
'Batch (4) Intended for batch servers, where processes can be executed on behalf of a user without their direct intervention; or for higher performance servers that process many clear-text authentication attempts at a time, such as mail or web servers. LogonUser does not cache credentials for this logon type.
'Service (5) Indicates a service-type logon. The account provided must have the service privilege enabled.
'Proxy (6) Indicates a proxy-type logon.
'Unlock (7) This logon type is intended for GINA DLLs logging on users who are interactively using the machine. This logon type allows a unique audit record to be generated that shows when the workstation was unlocked.
'NetworkCleartext (8) Preserves the name and password in the authentication packages, allowing the server to make connections to other network servers while impersonating the client. This allows a server to accept clear text credentials from a client, call LogonUser, verify that the user can access the system across the network, and still communicate with other servers.
'NewCredentials (9) Allows the caller to clone its current token and specify new credentials for outbound connections. The new logon session has the same local identify, but uses different credentials for other network connections.
'RemoteInteractive (10) Terminal Services session that is both remote and interactive.
'CachedInteractive (11) Attempt cached credentials without accessing the network.
'CachedRemoteInteractive (12) Same as RemoteInteractive. This is used for internal auditing.
'CachedUnlock (13) Workstation logon.


Class logonEventClass
	dim wmiType, invType, unixTime, strTime, vbsTime, strUserName, strDomainName
	
	Function setWmiTime(wmiTime)
		vbsTime = timeWmiToVbs(wmiTime)
		strTime = timeVbsToTimestamp(vbsTime)
		unixTime = timeVbsToUnix(vbsTime)		
	End function

	Function typeConvert(winType)
		wmiType=winType
		invType=-1
		Select case winType
			case 2
				invType=0
			case 11
				invType=0
			case 10
				invType=1
			case 12
				invType=1
		end select
	End function

	function init(logonEvent)
		init = false

		typeConvert(logonEvent.LogonType)
		if invType<0 then 
			debugMsg "Ignoring event " & objItem.LogonId & " with type " & logonEvent.LogonType
			exit function
		end if
		
		setWmiTime(objItem.startTime)
		'сценарий когда комп загрузился со сбитыми часами (2001-01-01 00:00:00), зафиксировал в журнале событие входа в 5 минут 1 января
		'а потом часы выставились правильно, и при отправке информации уже не видно сдвига по времени
		'в итоге не отправляем данные о событиях более чем месяц назад
		if (timeGetUnixEpoch-unixTime)>100*24*3600 then 
			debugMsg "Ignoring event " & objItem.LogonId & " with tooo old date " & strTime
			exit function
		end if
		
		strUserName=""
		strDomainName=""
		dim colList, objUser
		Set colList = objWmi.ExecQuery("Associators of {Win32_LogonSession.LogonId=" & logonEvent.LogonId & "} Where AssocClass=Win32_LoggedOnUser Role=Dependent" )       
		on error resume next
		For Each objUser in colList
			strUserName = objUser.Name
			strDomainName = objUser.Domain
		Next
		on error goto 0
		if strUserName="" or strDomainName="" then 
			debugMsg "Can't find event " & objItem.LogonId & " username "
			exit function
		end if

		init = true
	end function

	Function varDump()
		Msg "Logon: [type:" & wmiType & "->" & invType & ", time:" & strTime & ", unix:" & unixTime & " // " & strDomainName & "\" & strUserName &"]"
	End Function

	Function keyString()
		keyString = invType & "-" & unixTime & "-" & strUserName
	End Function



	'ищет запись этого логона в БД возвращает ID созданной/обновленной записи
	function setRecord(byVal comp)
		dim data, res, updRecord
		data="comp_name=" & comp & "&user_login=" & strDomainName & "\" & strUserName & "&time=" & unixTime & "&type=" & invType & "&local_time=" & timeGetUnixEpoch
	
		on error resume next
		res=postAuthXmlData(inventory_apihost & "/web/api/login-journal/push",data,inventory_user,inventory_password)
		on error goto 0
		if (getXmlResponseID(res) > -1) then
			updRecord="ok " & res
			Msg "Data sent: " & res & "; Database updated"
		else
			Msg "Error sending data ... retrying in next launch"
			Msg data
			Msg res
		end if	
	end function

	
End Class



dim logFile: logFile = WorkDir & scrName & ".log"
Msg "-" : Msg "Script started: "&scrName&" "&scrVer


dim wshNetwork: Set wshNetwork = CreateObject( "WScript.Network" )
dim strComputerName, strComputerDomain
dim objItems, objItem

Set objItems = objWmi.ExecQuery("Select * from Win32_ComputerSystem",,48)
For Each objItem in objItems
    strComputerDomain = objItem.Domain
	strComputerName = objItem.Name
Next


'почемуто вот этот вот запрос отдает мне 2 элемента. по собственно времени логона они практически идентичны
'отличаются в микросекундах, но все же отличны. потому данные кладем в словарь с точностью до секунд
Set objItems = objWmi.ExecQuery ("Select * from Win32_LogonSession")
dim objLogons: set objLogons = CreateObject("Scripting.Dictionary")
dim logonEvent
dim colList, objUser

For Each objItem in objItems
	if DEBUGMODE then
		WScript.Echo "AuthenticationPackage: " & objItem.AuthenticationPackage 
		WScript.Echo "Caption: " & objItem.Caption
		WScript.Echo "Description: " & objItem.Description
		WScript.Echo "InstallDate: " & objItem.InstallDate
		WScript.Echo "LogonId: " & objItem.LogonId
		WScript.Echo "Name: " & objItem.Name
		WScript.Echo "LogonType: " & objItem.LogonType
		WScript.Echo "StartTime: " & objItem.StartTime
		WScript.Echo "Status: " & objItem.Status
	end if

	'Set colList = objWMIService.ExecQuery("Associators of " _
	'	& "{Win32_LogonSession.LogonId=" & objItem.LogonId & "} " _
	'	& "Where AssocClass=Win32_LoggedOnUser Role=Dependent" )
        '
	'	For Each objUser in colList
	'		WScript.Echo "User: " & objUser.Name
	'		WScript.Echo "Domain: " & objUser.Domain
	'		WScript.Echo
	'	Next
	
	set logonEvent = new logonEventClass
	with logonEvent
		if (.init(objItem)) then		
			if not objLogons.exists(.keyString()) then
				objLogons.Add .keyString(), logonEvent
				.varDump
				.setRecord strComputerName & "." & strComputerDomain
			end if
		end if
	end with
	
Next
Msg "Script complete."

wscript.quit
</script>

<signature>
** SIG ** MIIIXwYJKoZIhvcNAQcCoIIIUDCCCEwCAQExDzANBglg
** SIG ** hkgBZQMEAgEFADB3BgorBgEEAYI3AgEEoGkwZzAyBgor
** SIG ** BgEEAYI3AgEeMCQCAQEEEHAFYRrOONQRoqMAEEvTUJAC
** SIG ** AQACAQACAQACAQACAQAwMTANBglghkgBZQMEAgEFAAQg
** SIG ** ATkoeom1HQfQMVcFa2gpAi5Bxk8/TvOmKyPl9hdxFfSg
** SIG ** ggWcMIIFmDCCA4CgAwIBAgIBAzANBgkqhkiG9w0BAQsF
** SIG ** ADBtMQswCQYDVQQGEwJSVTENMAsGA1UECAwEVXJhbDEU
** SIG ** MBIGA1UEBwwLQ2hlbHlhYmluc2sxETAPBgNVBAoMCFJl
** SIG ** dmlha2luMQswCQYDVQQLDAJJVDEZMBcGA1UEAwwQcmV2
** SIG ** aWFraW4tcm9vdC1DQTAeFw0yMzA1MjUxNTM3MDBaFw0y
** SIG ** NDA2MDMxNTM3MDBaMGMxCzAJBgNVBAYTAlJVMQ0wCwYD
** SIG ** VQQIDARVcmFsMQ0wCwYDVQQHDARDaGVsMREwDwYDVQQK
** SIG ** DAhSZXZpYWtpbjELMAkGA1UECwwCSVQxFjAUBgNVBAMM
** SIG ** DXJldmlha2luLWNvZGUwggEiMA0GCSqGSIb3DQEBAQUA
** SIG ** A4IBDwAwggEKAoIBAQCtsuYd7CVRsLwbN6ybLrnCr72O
** SIG ** nqGhfdASM37B9yC8+b5nnbw6EqDEN2IHpy32wOoThAlg
** SIG ** zPna/D5/VX/TYuLR/1vjW+vRQPKbJi8m97BMr8PemMWl
** SIG ** w6mjl9x4qW0x4irIwXra/Z4R34BgrY8ZACZRah0riiWY
** SIG ** GXPvCw3ZjNYMXRJF4rVKJ6c/PNg1bNlML1Q8oHcy3MPC
** SIG ** CVCHF/Qf3Bl/l76GKJhylViC5/ZiX34LfzCopdK1xnnY
** SIG ** 45cP1c83pQH2IE3ucjGMwzWDYCwTNAeYi69aaK40fGHC
** SIG ** Z9EJg6sS1RnEyCpp+Sj23T/GOJyTxM4kaiPmlMDZoCAq
** SIG ** UndLk6HVAgMBAAGjggFLMIIBRzAJBgNVHRMEAjAAMBEG
** SIG ** CWCGSAGG+EIBAQQEAwIFoDAzBglghkgBhvhCAQ0EJhYk
** SIG ** T3BlblNTTCBHZW5lcmF0ZWQgQ2xpZW50IENlcnRpZmlj
** SIG ** YXRlMB0GA1UdDgQWBBSXtltT7BkMs4W7USOsFdk+mc0S
** SIG ** HjAfBgNVHSMEGDAWgBSNQkTnQD4Z5d3UogsBh0kUyrwl
** SIG ** pzAOBgNVHQ8BAf8EBAMCBeAwJwYDVR0lBCAwHgYIKwYB
** SIG ** BQUHAwIGCCsGAQUFBwMEBggrBgEFBQcDAzA4BgNVHR8E
** SIG ** MTAvMC2gK6AphidodHRwOi8vcGtpLnJldmlha2luLm5l
** SIG ** dC9jcmwvcm9vdC1jYS5jcmwwPwYIKwYBBQUHAQEEMzAx
** SIG ** MC8GCCsGAQUFBzAChiNodHRwOi8vcGtpLnJldmlha2lu
** SIG ** L25ldC9yb290LWNhLmNydDANBgkqhkiG9w0BAQsFAAOC
** SIG ** AgEAix6Hc2aULCO6RiT4W5PIiB9zQgA4BGT3W5YdSttn
** SIG ** gGhnmWDEfT2bhB/ZnRLkrtZeL/sYDj94FIfKZMvFTsNN
** SIG ** CUeDNiV9hQyJrsrI9Gq3nkgcnCOGc/9mqqL7ItS33s1M
** SIG ** ltSXVA7sLhoQ65yPrP70kd3681COUsCYOq7hroIR3Th4
** SIG ** L8INGLvUR+Xll1sunIHrnuiTD/GZFNemDec0f3n8mNKp
** SIG ** 5KiWuYlNYv0Zg//rTvCZfk2Y74Mk/2lCeABVKcQoJai+
** SIG ** XiSN0mq1b6RlFmfbiuzU3iudZ3SKHKEd3reGBXZxD7b1
** SIG ** QubveA17QKbgzwjT6DX9ISFjbIOuB9HUo3Bl7VLZ4DyH
** SIG ** 2mt0z+UC1zpE9DLFzoawf4f5/KN6mixGX9Q7tSQQCOKo
** SIG ** Jiyk7Y+0aLXhK7RmJdDK3vIieJkXSx0ip1SXdRYgr0sQ
** SIG ** VsNq2D2SYJ0A1r2wWJ4sNuiHnDuxWuxLsAdC0rZTlKis
** SIG ** 21i4uOIr3BCj2MFdTTdkeX5xB979r/8MLBdrDlzoVxMz
** SIG ** tEWwXdNlqiCQosIMVq44bJF1zjFPD6pYk0JgEF9y8wTd
** SIG ** G2LyGFjTqJYyCrKrWFkQa8GX6pazj4EarEpNjdVC6IXJ
** SIG ** YRa4vRqUEWfS9WeTGlIR9hJyqtHKAc9N82lwrhTlPhh+
** SIG ** lkL15ZPRXnnd5aICNgQpndNfyBIxggIbMIICFwIBATBy
** SIG ** MG0xCzAJBgNVBAYTAlJVMQ0wCwYDVQQIDARVcmFsMRQw
** SIG ** EgYDVQQHDAtDaGVseWFiaW5zazERMA8GA1UECgwIUmV2
** SIG ** aWFraW4xCzAJBgNVBAsMAklUMRkwFwYDVQQDDBByZXZp
** SIG ** YWtpbi1yb290LUNBAgEDMA0GCWCGSAFlAwQCAQUAoHww
** SIG ** EAYKKwYBBAGCNwIBDDECMAAwGQYJKoZIhvcNAQkDMQwG
** SIG ** CisGAQQBgjcCAQQwHAYKKwYBBAGCNwIBCzEOMAwGCisG
** SIG ** AQQBgjcCARUwLwYJKoZIhvcNAQkEMSIEIAV+psGyP70E
** SIG ** 3YpTCfKRI471zIR9LgBMUHfy0N4GuSAuMA0GCSqGSIb3
** SIG ** DQEBAQUABIIBADN7GOzN7LJAUMBFZJiTh1KT3RTa2e7U
** SIG ** Mp70lEmvDXymVsYKgOjXJivjQ2434LNaKzzaSC5xBofF
** SIG ** pbaukdPelYZ+eJd/NQlQDWy2V+aKHYwTLvshwVfP3wBh
** SIG ** 4XCMrBsGES1vk+RGvN9mDvPCsGCMDwAMvRApHn2jO7Qk
** SIG ** K61LZqZyjV0pV+v41ukA6Fcp4O3VT7Tw+ySY15831BzV
** SIG ** 3TSTmlOGia6Fn0M5qEeJpGTMRHRN80jKwtUqy+rBe3Qe
** SIG ** vZnbMODGxe5SVP5+6bcS5bI4msxwdy4Jr3Shml2Lhj5C
** SIG ** ASpEhou/WCWG5ldyKF5bNA6hvzvUnqKXee+dAEMWLaySdxE=
</signature>
</job>